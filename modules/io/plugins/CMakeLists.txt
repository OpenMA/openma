# IF(OPENMA_BUILD_SHARED_LIBS)
#   MESSAGE(FATAL_ERROR "The IO handler dynamic loading mechanism is not yet implemented. You cannot build this module as a shared library.")
# ENDIF()

SET(OPENMA_STATIC_IO_PLUGINS_SRCS "")
SET(OPENMA_STATIC_IO_PLUGIN_LOADER_INCLUDES "")
SET(OPENMA_STATIC_IO_PLUGIN_LOADER_COMMANDS "")

FILE(GLOB FORMATS_PATHS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "${CMAKE_CURRENT_SOURCE_DIR}/*")
FOREACH(_format_path ${FORMATS_PATHS})
  SET(OPENMA_CURRENT_FORMAT_PATH "${CMAKE_CURRENT_SOURCE_DIR}/${_format_path}")
  IF(IS_DIRECTORY ${OPENMA_CURRENT_FORMAT_PATH})
    FILE(GLOB HANDLERS_PATHS RELATIVE ${OPENMA_CURRENT_FORMAT_PATH} "${OPENMA_CURRENT_FORMAT_PATH}/*")
    FOREACH(_handler_path ${HANDLERS_PATHS})
      IF(IS_DIRECTORY ${OPENMA_CURRENT_FORMAT_PATH}/${_handler_path})
        SET(OPENMA_IO_PLUGIN_NAME "" CACHE INTERNAL "")
        SET(OPENMA_IO_PLUGIN_SRCS "" CACHE INTERNAL "")
        ADD_SUBDIRECTORY(${_format_path}/${_handler_path})
        IF (NOT OPENMA_IO_PLUGIN_NAME OR NOT OPENMA_IO_PLUGIN_SRCS)
          MESSAGE(FATAL_ERROR "Missing plugin name or plugin sources. Please, verify the CMake configuration script")
        ENDIF()
        SET(OPENMA_STATIC_IO_PLUGINS_SRCS ${OPENMA_STATIC_IO_PLUGINS_SRCS} ${OPENMA_IO_PLUGIN_SRCS} CACHE INTERNAL "")
        STRING(TOLOWER ${OPENMA_IO_PLUGIN_NAME} OPENMA_IO_PLUGIN_HEADER)
        SET(OPENMA_STATIC_IO_PLUGIN_LOADER_INCLUDES
          "${OPENMA_STATIC_IO_PLUGIN_LOADER_INCLUDES}#include \"${_format_path}/${_handler_path}/${OPENMA_IO_PLUGIN_HEADER}.h\"\n")
        SET(OPENMA_STATIC_IO_PLUGIN_LOADER_COMMANDS
          "${OPENMA_STATIC_IO_PLUGIN_LOADER_COMMANDS}  plugin = new ${OPENMA_IO_PLUGIN_NAME}; manager->add(plugin);\n")
      ENDIF()
    ENDFOREACH()
  ENDIF()
ENDFOREACH()


# IF(OPENMA_BUILD_SHARED_LIBS)
#   SET(OPENMA_STATIC_IO_PLUGIN_LOADER_INCLUDES "")
#   SET(OPENMA_STATIC_IO_PLUGIN_LOADER_COMMANDS "")
# ENDIF()

CONFIGURE_FILE(${OPENMA_CMAKE_MODULE_PATH}/templates/staticiopluginloader.h.in
               ${CMAKE_CURRENT_BINARY_DIR}/staticiopluginloader.h @ONLY IMMEDIATE)