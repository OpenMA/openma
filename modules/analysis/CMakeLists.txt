SET(OPENMA_ANALYSIS_SRCS
  src/_analysis.cpp
  src/algorithm.cpp
  src/taskeventdetection.cpp
  src/taskeventdetectionplugin.cpp
  src/taskeventdetector.cpp
)

# Variable OPENMA_STATIC_IO_PLUGINS_SRCS is used for static build
SET(OPENMA_STATIC_ANALYSIS_PLUGINS_SRCS "" CACHE INTERNAL "")
ADD_SUBDIRECTORY("plugins")

ADD_LIBRARY(analysis ${OPENMA_LIBS_BUILD_TYPE} ${OPENMA_ANALYSIS_SRCS} ${OPENMA_STATIC_ANALYSIS_PLUGINS_SRCS})
TARGET_LINK_LIBRARIES(analysis body)
TARGET_INCLUDE_DIRECTORIES(analysis PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/plugins>
  $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>
  $<BUILD_INTERFACE:${CMAKE_BINARY_DIR}/include>
  $<INSTALL_INTERFACE:include>
)
GENERATE_EXPORT_HEADER(analysis
                       BASE_NAME OPENMA_ANALYSIS
                       EXPORT_FILE_NAME include/openma/analysis_export.h)

SET_TARGET_PROPERTIES(analysis PROPERTIES ${OPENMA_LIBRARY_PROPERTIES})
IF (NOT ${OPENMA_BUILD_SHARED_LIBS})
  SET_TARGET_PROPERTIES(analysis PROPERTIES COMPILE_FLAGS -DOPENMA_BASE_STATIC_DEFINE)
ENDIF()

IF(BUILD_LANGUAGE_BINDINGS)
  ADD_SUBDIRECTORY(swig)
ENDIF()

IF(BUILD_UNIT_TESTS)
  ADD_SUBDIRECTORY(test)
ENDIF()

INSTALL(TARGETS analysis EXPORT OpenMATargets
  LIBRARY DESTINATION lib
  ARCHIVE DESTINATION lib
  RUNTIME DESTINATION lib  
  INCLUDES DESTINATION lib
)